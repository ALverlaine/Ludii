(define "ColumnSize" 6 )

(define "EmptyTop" (intersection (sites Top) (sites Empty)))

(define "LastColumn" (sites Column (column of:(last To))))

(define "Distance" (count Sites in:(intersection (sites Empty) "LastColumn")))

(define "Drop" (slide (from (last To)) S (between (exact "Distance"))))

//------------------------------------------------------------------------------

(game "Connect Four"  
    (players 2)  
    (equipment { 
        <Model:board>
        (piece "Disc" Each) 
    }) 
    (rules
        <Model:rules>
    )
)

//------------------------------------------------------------------------------

(option "Model" <Model> args:{ <board> <rules> }
    {
    (item "1D"      
        <(board (rectangle 1 7))>
        <    
        (play (add (mover) to:(forEach (sites Board) if:(< (size Stack at:(to)) "ColumnSize")) stack:True))
        (end (if (is Line 4) (result Mover Win)))
        >
        ""
    )**   
    (item "2D"         
        <(board (rectangle 6 7))>
        <
        (play (add (mover) to:("EmptyTop") (then "Drop")))
        (end (if (is Line 4 through:(last To afterConsequence:True)) (result Mover Win)))
        >
        ""
    )   
    (item "Sites N"         
        <(board (rectangle 6 7))>
        <
        (play 
            (add 
                to:(intersection 
                    (sites Empty) 
                    (union 
                        (sites Bottom) 
                        (sites 
                            Direction 
                            from:(sites Occupied by:All) 
                            N 
                            distance:1
                        )
                    )
                )
            )
        )
        (end (if (is Line 4) (result Mover Win)))
        >
        ""
    )   
    (item "Dots A"         
        <(board (rectangle 6 7)) (piece "Dot" Neutral)>
        <
        (start 
            (place "Dot0" (sites Bottom))
        ) 
        (play 
            (select 
                (from (sites Occupied by:Neutral))
                (then 
                    (and 
                        {
                        (remove (last To))
                        (add (id "Disc" Mover) at:(last To))
                        (if (!= (ahead (last To) N) (last To)) 
                            (add (id "Dot0") at:(ahead (last To) N))
                        )
                        }
                    )
                )
            ) 
        )
        (end (if (is Line 4 what:(id "Disc" Mover) ) (result Mover Win)))
        >
        ""
    )   
    (item "Dots B"         
        <(board (rectangle 6 7)) (piece "Dot" Neutral)>
        <
        (start 
            (place "Dot0" (sites Bottom))
        ) 
        (play 
            (select (from (sites Occupied by:Neutral))
                (then (and 
                        {
                        (if (!= (ahead (last To) N) (last To)) 
                            (fromTo (from (last To)) (to (ahead (last To) N)))
                            (remove (last To))
                        )
                        (add (id "Disc" Mover) at:(last To))
                        }
                    )
                )
            )
        )
        (end (if (is Line 4 what:(id "Disc" Mover)) (result Mover Win)))
        >
        ""
    )   
})

(metadata
    
    (info
        {
        (description "Connect Four is a two-player connection game that was designed by Howard Wexler and Ned Strongin, and was first sold under this name by Milton Bradley in 1974.")
        (aliases {"Four Up" "Plot Four" "Find Four" "Four in a Row" "Four in a Line" "Drop Four" "Gravitrips"})
        (rules "Connect 4 is played on a vertically placed grid of 7x6, where colored disks are dropped from the top of the grid. Players alternate dropping discs, which fall to the bottom of the column in which they are dropped. The first player to create a row of four disks in their color wins.")
        (source "https://en.wikipedia.org/wiki/Connect_Four")
        (version "0.9.3")
        (classification "board/space/line")
        (author "Howard Wexler, Ned Strongin")
        (publisher "Milton Bradley / Hasbro")
        (credit "Eric Piette")
        (date "1974")
        }
    )
    
    (graphics {
        (piece Scale "Dot" 0.1)
        (player Colour P1 (colour Yellow))
        (player Colour P2 (colour Red))
        (noAnimation True)
        (selectClosestPiece True)
    })    
)
