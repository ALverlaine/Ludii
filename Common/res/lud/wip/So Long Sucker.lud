(define "OwnerInLastStack" 
    (results 
        from:(last To) 
        to:(forEach Level at:(last To)) 
        (who at:(last To) level:(to))
    )		
)

(define "All4PlayersInLastStack" 
    (and {
        (is In 1 ("OwnerInLastStack"))
        (is In 2 ("OwnerInLastStack"))
        (is In 3 ("OwnerInLastStack"))
        (is In 4 ("OwnerInLastStack"))
    })
)

(define "OwnerMostRecentPlayedChip"
    (arrayValue 
        (results 
            from:(last To) 
            to:(forEach Level at:(last To) FromBottom 
                if:(not 
                    (is In 
                        (who at:(last To) level:(level))
                        (results 
                            from:(last To) 
                            to:(forEach Level at:(last To) FromBottom startAt:(+ 1 (level)))
                            (who at:(last To) level:(to))
                        )
                    )
                )
            )
            (who at:(last To) level:(to))
        )
        index:0
    )
)

(define "ACaptureIsMade"
    (and
        (> (size Stack at:(last To)) 1)
        (= 
            (who at:(last To) level:(- (size Stack at:(last To)) 1))
            (who at:(last To) level:(- (size Stack at:(last To)) 2))
        )
    )	
)

(define "ACaptureWasMade"
    (= (var "CaptureWasMade") 1)
)

(define "NoChip"
	(and {
		(is Empty (handSite Mover))
		(is Empty (handSite Mover 1))
		(is Empty (handSite Mover 2))
		(is Empty (handSite Mover 3))
		(is Empty (handSite Mover 4))
	})
)

//-------------------------------------------------------------------------

(game "So Long Sucker"  
    (players 4)  
    (equipment { 
        (board (rectangle 4 7))
        (hand Each size:5)
        (piece "Marker" Each)
    })  
    (rules
        (start {
            (place Stack "Marker1" (handSite P1) count:7)
            (place Stack "Marker2" (handSite P2) count:7)
            (place Stack "Marker3" (handSite P3) count:7)
            (place Stack "Marker4" (handSite P4) count:7)
        })
        
        (play 
            (if ("ACaptureWasMade")
                (forEach Level (last To)
                    (move Remove (last To) level:(level))
                    (then
                    	(and {
	                        (forEach Level (last To)
	                            (fromTo
	                                (from (last To) level:(level))
	                                (to 
	                                    (if (is Mover (who at:(last To) level:(level)))
	                                        (handSite Mover 0)
	                                        (handSite Mover (who at:(last To) level:(level)))
	                                    )
	                                )
	                            )
	                        )
	                        (moveAgain)
	                        (set Pending)
                            (set Var "CaptureWasMade" 0)
                    	})
                    )
                )
                (if (and (not (is Pending)) (is Prev Mover))
                    (if ("All4PlayersInLastStack")
                        (move Set NextPlayer (player ("OwnerMostRecentPlayedChip")))
                        (forEach Player
                            (if (not (is In (player) ("OwnerInLastStack")))
                                (move Set NextPlayer (player (player)))
                            )
                        )
                    )
                    (if ("NoChip")
                    	(move Pass 
                    		(then 
                    			(and
                    			(set Var "Defeat" (mover))
                    			(set NextPlayer (player (prev)))
                    			)
                    		)
                    	)
	                    (or {
		                    (move 
		                        (from (sites Hand Mover))
		                        (to (sites Board))
		                        stack:True
		                        (then
		                            (if ("ACaptureIsMade")
		                                (and
		                                    (set Var "CaptureWasMade" 1)
		                                    (set NextPlayer (player (who at:(last To))))
		                                )
		                                (moveAgain)
		                            )
		                        )
		                    ) 
		                    (move Remove
		                    	(forEach (sites Hand Mover) 
		                    		if:(and 
		                    				(!= (site) (handSite Mover 0))
		                    				(is Occupied (site))
		                    			)
		                    	)
		                    )
		                    (forEach Player 
		                    	(if (not (is Mover Player))
				                    (move
				                    	(from
					                    	(forEach (sites Hand Mover) 
					                    		if:(and 
					                    				(!= (site) (handSite Mover 0))
					                    				(is Occupied (site))
					                    			)
					                    	)
					                    )
				                    	(to (handSite Player (who at:(from))))
				                    )
				                 )
		                    )
	                    })
                    )
                )
            )
        )
        
        (end 
        	(if (is Mover (var "Defeat")) (result Mover Loss))
        )
    )
)

(metadata
    (info {
        
    })
    
    (graphics {
        (no Board)
        (stackType 0 Default 0.5)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
    })
    
)
