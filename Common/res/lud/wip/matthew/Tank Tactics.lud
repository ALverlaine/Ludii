
(define "Move"
	(move Step (to if:(is Empty (to))) (then "PostActionUpdate"))
)

(define "Shoot"
	(move Select
		(from)
		(to
			(intersection 
				(sites Occupied by:All container:"Board")
            	(sites Distance from:(from) (range 1 (state at:(last From))))
            )
		)
		(then 
			(and
				(if	(= (value Piece at:(last To)) 1)
					(remove (last To))
					(set Value at:(last To) (- (value Piece at:(last To)) 1))
				)
				"PostActionUpdate"
			)
		)
	)
)

(define "Trade"
	(move Select
		(from)
		(to
			(intersection 
				(sites Occupied by:All container:"Board")
            	(sites Distance from:(from) (range 1 (state at:(last From))))
            )
		)
		(then 
			(and
				(addScore (player (who at:(last To))) 1)
				"PostActionUpdate"
			)
		)
	)
)

(define "Upgrade"
	(move Select
		(from)
		(to)
		(then 
			(and
				(set State at:(last To) (+ 1 (state at:(last To))))
				"PostActionUpdate"
			)
		)
	)
)

(define "Pass"
	(move Pass
    	(then
			(addScore
		        Mover
		        1
		    )
		)
	)
)

(define "PostActionUpdate"
	(if (> (score Mover) 1)
		(and 
			(addScore
		        Mover
		        -1
		    )
		    (moveAgain)
    	)
    )
)

//------------------------------------------------------------------------------

(game "Tank Tactics"  
    (players 2)  
    (equipment { 
        (board (square 10)) 
        (hand Each)
        (piece "Queen" Each (or {"Move" "Shoot" "Trade" "Upgrade"}))
        (piece "Dot" Neutral)
    })  
    (rules 
    	(meta (passEnd NoEnd))
        (start { 
        	(set Score Each 1)
            (place "Queen1" (handSite P1) state:2 value:3)
            (place "Queen2" (handSite P2) state:2 value:3)
        })
        phases:{
        (phase "Placement"
            (play
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty)) 
                )  
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        (phase "Movement" 
	        (play 
	        	(or 
	            	(forEach Piece)
	            	"Pass"
	            )
	        )
	    )}
        (end 
            (forEach NonMover 
                if:(= (count Pieces Player) 0) (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Invented in 1988 by Walter Zamkauskas and first published in the Argentine magzine El Acertijo in December 1992.")
        (rules "Played on a 10x10 board. Each player has four Amazons (chess queens), with other pieces used as arrows. Two things happen on a turn: an amazon moves like a Chess queen, but cannot cross or enter a space occupied by another amazon or arrow. Then, it shoots an arrow to any space on the board that is along the path of a queen's move from that place. The last player able to make a move wins.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Game_of_the_Amazons\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.2.5")
        (classification "board/space/blocking")
        (author "Walter Zamkauskas")
        (credit "Eric Piette")
        (origin  "This game is from Argentina, on 1988.")
        }
    )
    
    (graphics {
        (show Piece Value CornerLeft valueOutline:true)
        (show Piece State CornerRight valueOutline:true)
    })
    
)
