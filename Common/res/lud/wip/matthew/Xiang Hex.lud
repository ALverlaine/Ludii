(define "JiangSameColumnWithNoPieceBetween"
    (if (= (column of:(where "Jiang" P1) ) (column of:(where "Jiang" P2) ))
        (not ("NoSites" (forEach (sites Direction from:(where "Jiang" P1) N stop:(= (to) (where "Jiang" P2))) if:(not (is Empty (site))))))
        True
    )
)

(define "EnemyCannotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "Jiang" Next))) ))
)

(define "StepMoveOnThePalace" 
    (move
        Step
        #1 
        (to 
            if:(and 
                (is In (to) (sites Mover "Palace") ) 
                (not (is Friend (who at:(to))))
            ) 
            (apply (remove (to) ))
        )
    ) 
)

//------------------------------------------------------------------------------

(game "Xiang Hex" 
    (players {(player N) (player S)})  
    (equipment { 
         (board (renumber Cell (rotate 90 (remove (hex Rectangle 9 11) cells:{0 1 11 21 63 74 85 84 94 93 83 73 31 20 9 10}))))
        
        // General
        (piece "Jiang" Each ("StepMoveOnThePalace" Orthogonal)) 
        
        // Guard
        (piece "Shi" Each ("StepMoveOnThePalace" Diagonal)) 
        
        // Elephant
        (piece "Xiang" Each 
            (move
                Slide
                Diagonal 
                (between 
                    (exact 2) 
                    if:(and { 
                        (is In (between) (sites Mover "Home") ) 
                        (is Empty (between)) 
                    })
                )
                (to 
                    if:(is Enemy (who at:(to))) 
                    (apply (remove (to) ))
                ) 
            )
        ) 
        
        // Horse
        (piece "Ma" Each 
            (forEach Direction 
                Orthogonal 
                (to 
                    if:(is Empty (to)) 
                    (apply 
                        (forEach Direction 
                            (from (to)) 
                            (directions {FR FL} of:All) 
                            (if (not (is Friend (who at:(to)))) 
                                (move 
                                    (from) 
                                    (to 
                                        (apply 
                                            if:(is Enemy (who at:(to)))
                                            (remove (to))
                                        )
                                    )
                                )
                            )
                        ) 
                    )
                )
            ) 
        )
        
        // Chariot
        (piece "Ju" Each 
            (move
                Slide
                Orthogonal 
                (to 
                    if:(is Enemy (who at:(to))) 
                    (apply (remove (to) ) )
                )
            )
        )
        
        // Canon
        (piece "Pao" Each 
            (or 
                (move Slide Orthogonal) 
                (move
                    Hop
                    Orthogonal 
                    (between 
                        before:(count Rows) 
                        after:(count Rows)  
                        if:(not (is Empty (between)))
                    )
                    (to 
                        if:(is Empty (to)) 
                        (apply 
                            if:(is Enemy (who at:(to))) 
                            (remove (to) )
                        )
                    )  
                ) 
            )
        )
        
        // Soldier
        (piece "Zu" Each
            (move
                Step
                (if 
                    (is In (from) (sites Mover "Home")) 
                    Forward 
                    (directions {Forward Leftwards Rightwards})
                ) 
                (to 
                    if:(not (is Friend (who at:(to)))) 
                    (apply (remove (to)))
                )
            )
        )
        
        (regions "PalaceP1" P1 { 34 24 45 35 46 36 25 } )
        (regions "PalaceP2" P2 { 44 33 43 54 53 42 32 } )
        (regions "Home" P1 (forEach (sites Board) if:(< (site) 41)))
        (regions "Home" P2 (forEach (sites Board) if:(> (site) 37)))
    })  
    
    (rules 
        (start { 
            (place "Jiang1" coord:"A1") (place "Shi1" {"A2" "B1"}) (place "Xiang1" {"A3" "C1"}) (place "Ma1" {"A4" "D1"}) (place "Ju1" {"A5" "E1"}) (place "Pao1" {"B5" "E2"}) (place "Zu1" {"B6" "C5" "D4" "E3" "F2"})
            (place "Jiang2" coord:"K11") (place "Shi2" {"J11" "K10"}) (place "Xiang2" {"I11" "K9"}) (place "Ma2" {"H11" "K8"}) (place "Ju2" {"G11" "K7"}) (place "Pao2" {"G10" "J7"}) (place "Zu2" {"F10" "G9" "H8" "I7" "J6"})
        })
        
        (play (do 
                (forEach Piece) 
                ifAfterwards:(and 
                    ("JiangSameColumnWithNoPieceBetween")
                    (not ("IsInCheck" "Jiang" Mover))
                )
            )
        )
        
        (end {
            (if (and 
                    ("IsInCheck" "Jiang" Next) 
                    ("EnemyCannotMove")
                ) 
                (result Mover Win)
            ) 
            (if (no Moves Next) (result Mover Win)) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Xiangqi, or \"Elephant Game,\" is a Chinese game ultimately derived from Indian Chaturanga. Its exact date of origin is disputed, but it has been popular in China for over a thousand years.")
        (aliases {"Siang K'i"})
        (rules "Played on a board with 9x10 intersecting lines. Pieces are placed on the intersections of the lines. Centered on opposite sides of the boards are two areas, known as the gong (\"castle\"). Bisecting the board is a blank space known as the he (\"river\"). Pieces are double-sided, with different colors and names of the pieces indicating to which player they belong. Jiang(\"general\")/shuai(\"marshal\")(1): may move one space orthogonally and cannot leave the gong except when opposite the opponent's jiang/shuai, in which case the piece can capture the opponent's jiang/shuai. Shi(\"guard\")(2): moves one space diagonally and cannot leave the gong. Xiang(\"elephant\")/xiang(\"minister\")(2): move two places diagonally and cannot cross the river. Ma(\"horse\")(2): move orthogonally one and then diagonally one space. Can be blocked by a piece next to it orthogonally. Ju(\"chariot\"(2): moves any number of spaces orthogonally. Pao(\"catapult\")/pao(\"cannon\") (2): can move orthogonally any number of spaces, captures by jumping one piece (of either player). Zu(\"private\"/zu(\"soldier\")(5): can move one space forward. Once they cross the river, can also move one space horizontally. The goal is to checkmate the opponent's jiang/shuai.")
                (source "Murray 1913: 121-134.")
                (version "1.2.5")
                (classification "board/war/replacement/checkmate/xiangqi")
                (credit "Eric Piette")
                (origin  "This game was played in China, from around 779 to 1127.")
                }
            )
            
            (graphics {
                (player Colour P1 (colour White))
                (player Colour P2 (colour Red))
                //(board Style Xiangqi)
                (piece Families {"Defined" "Western"})
                (piece ExtendName P2 "_black")
                (region Colour "Home" P1 (colour Green))
                (region Colour "Home" P2 (colour Red))
            })
            
        )
