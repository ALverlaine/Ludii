(define "ThrowValue" (mapEntry "Throw" (count Pips)))
(define "Throw1" (= ("ThrowValue") 1))
(define "SiteToMoveOnTrack" (trackSite Move #1 steps:#2))
(define "HyenaOnTrack" (trackSite Move from:("WhereHyena") #1 steps:#2))
(define "HyenaPhaseDone" (or 
        (= ("WhereHyena") ("Center"))
        (and
            (!= 0 (what at:("Center")))
            (>= 1 (count Sites in:(difference (sites Occupied by:All) ("WhereHyena"))))
        )
    )
)

(define "WhereHyena" (where "Hyena" Neutral))
(define "LevelHyena" (where Level "Hyena" Neutral at:("WhereHyena")))
(define "StateHyena" (state at:("WhereHyena") level:("LevelHyena")))

(define "Center" 0)

(define "MoveHyena"
    (move
        (from ("WhereHyena") level:("LevelHyena"))
        (to ("HyenaOnTrack" "Track" ("ThrowValue")))
        (then
            (and
                (if (!= (mover) "StateHyena")
                    (set State at:("WhereHyena") level:("LevelHyena") (mover))
                )
                (if (= (last To) ("Center"))
                    (forEach Site (sites Track "Track" from:(last From) to:(last To))
                        (if (!= (site) (last To))
                            (forEach Level (site) FromTop
                                (remove (site) level:(level)) 
                            )
                        )
                    )
                    (forEach Site (sites Track "Track" from:(last From) to:(last To))
                        (if (!= (site) (last To))
                            (forEach Level (site) FromTop
                                (fromTo 
                                    (from (site) level:(level))
                                    (to (last To))
                                )
                            )
                        )
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Ishighan"
    (players <Player:number>)
    (equipment {
        (board 
            (spiral turns:5 sites:86) 
            {
            (track "Track" {86..0} directed:True)
            }
            use:Vertex
        )
        (dice d:2 from:0 num:4)
        (map "Throw" {(pair 0 4) (pair 1 3) (pair 2 0) (pair 3 1) (pair 4 6)})
        (piece "Stick" Each
            (move
                (from (from) level:(level)
                    if:(or 
                        (= 1 (state at:(from) level:(level)))
                        ("Throw1")
                    )
                )
                (to
                    ("SiteToMoveOnTrack" "Track" ("ThrowValue"))
                    if:True
                )
                (then
                    (if (!= 1 (state at:(last To) level:(last LevelTo)))
                        (set State at:(last To) level:(last LevelTo) 1)
                    )
                )
            )
        )
        (piece "Hyena" Neutral)
        (hand P1)
    })
    (rules 
        (start {
            (place Stack items:<Player:init> (handSite P1))
            (place Stack "Hyena0" (handSite P1))
        })
        
        (play 
            (do (roll) 
                next:(if (= ("Center") (where "Stick" Mover))
                    (if (< (value Player Mover) 2)
                        (move Pass (then (set Value Mover (+ 1 (value Player Mover)))))
                        (if (or (= (mover) ("StateHyena")) (= 0 ("StateHyena")))
                            (if (!= 0 ("ThrowValue")) ("MoveHyena"))
                        )
                    )
                    (if (!= 0 ("ThrowValue")) (or (forEach Piece) (forEach Piece container:1)))
                )
            )
        )
        (end {
            (forEach Player
                if:("NoPiece" Player) 
                (result Player Loss)
            )
            (forEach Player
                if:("HyenaPhaseDone")
                (result Player Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <number> <init>}
    {
    (item "2" <2> <{"Stick2" "Stick1"}> "The game has 2 players.") 
    (item "3" <3> <{"Stick3" "Stick2" "Stick1"}> "The game has 3 players.")  
    (item "4" <4> <{"Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 4 players.")*  
    (item "5" <5> <{"Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 5 players.") 
    (item "6" <6> <{"Stick6" "Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 6 players.") 
    (item "7" <7> <{"Stick7" "Stick6" "Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 7 players.") 
    (item "8" <8> <{"Stick8" "Stick7" "Stick6" "Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 8 players.") 
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        }
    )
    
    (graphics {
        (board Style Spiral)
        (stackType 0 Ground)
        (stackType 1 Ground)
        (piece Rename piece:"Hyena" "Marker")
        (piece Colour "Die" state:1 fillColour:(colour Green))
        (piece Scale "Stick" 0.5)
        (piece Scale "Hyena" 0.5)
        (hand Placement P1 scale:0.2 offsetX:0.7 offsetY:0.9)
        (player Colour Neutral (colour Black))
        (hand Placement P1 scale:0.2 offsetX:0.7 offsetY:0.9)
    })
    
)

