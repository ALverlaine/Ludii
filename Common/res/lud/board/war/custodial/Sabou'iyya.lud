(define "IsConfigurationToCustodialAgain" 
    (and 
        (is Empty (to)) 
        (or {
            (and
                (is Enemy (who at:(ahead (to) W)))
                (is Friend (who at:(ahead (to) steps:2 W)))
            )
            (and
                (is Enemy (who at:(ahead (to) E)))
                (is Friend (who at:(ahead (to) steps:2 E)))
            )
            (and
                (is Enemy (who at:(ahead (to) N)))
                (is Friend (who at:(ahead (to) steps:2 N)))
            )
            (and
                (is Enemy (who at:(ahead (to) S)))
                (is Friend (who at:(ahead (to) steps:2 S)))
            )
        })
    )	
)

(define "CanCaptureAgain"
    (can Move
        (step
            (from (last To))
            Orthogonal 
            (to if:("IsConfigurationToCustodialAgain"))
        )
    )	
)

//------------------------------------------------------------------------------

(game "Sabou'iyya"
    (players 2)
    (equipment {
        (board (square 5))
        (piece "Marker" Each 
            (move Step Orthogonal (to if:(is Empty (to)))
                (then 
                    (custodial 
                        (from (last To))
                        Orthogonal
                        (between
                            (max 1)
                            if:(is Enemy (who at:(between)))
                            (apply (remove (between)))
                        )
                        (to if:(is Friend (who at:(to))))
                        (then
                            (if ("CanCaptureAgain")	
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        (hand Each)
    })
    (rules
        (start (place "Marker" "Hand" count:12))
        
        phases:{
        (phase "Placement"
            (play
                (move 
                    (from (handSite Mover))
                    (to 
                        (if (= 0 (count Pieces Mover in:(sites Board)))
                            (intersection 
                                (sites Empty)
                                (sites Around (centrePoint) Orthogonal)
                            )
                            (difference (sites Board) (centrePoint)) 
                        )
                        if:(is Empty (to))
                    )
                    (then
                        (if (not (is Mover Prev)) (moveAgain))
                    )
                )
            )
            (nextPhase Mover (is Empty (handSite Mover)) "Capture")
        )
        (phase "Capture"
            (play 
                (if ("SameTurn")
                    (or
                        (move Step
                            (from (last To))
                            Orthogonal 
                            (to if:("IsConfigurationToCustodialAgain"))	
                        (then 
                            (custodial 
                                (from (last To))
                                Orthogonal
                                (between
                                    (max 1)
                                    if:(is Enemy (who at:(between)))
                                    (apply (remove (between)))
                                )
                                (to if:(is Friend (who at:(to))))
                                (then
                                    (if ("CanCaptureAgain")	
                                        (moveAgain)
                                    )
                                )
                            )
                        )
                    )
                    (move Pass)
                )
                (forEach Piece) 
            )
        )	
    )
    }
    (end (if (no Moves Next) (result Mover Win)))
)	
)

//--------------------------------------

(metadata 
    (info
        {
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
)
