(define "ThrowValue" (mapEntry (count Pips)))
(define "SiteToMoveOnTrack" (trackSite Move steps:("ThrowValue")))
(define "ActivatePiece" (set State at:(last To) 0))
(define "ActivatedPiece" (= (state at:(from)) 0))
(define "InactivePiece" (= (state at:(from)) 1))
(define "AllowToActivate" (= ("ThrowValue") 1))
(define "AllowToReplay" 
		(and
			(is In ("ThrowValue") (sites {1 4 6 5}))
			(!= 5 (count Pips))
		)
)

//------------------------------------------------------------------------------

(game "Issiren"
    (players 2)  
    (equipment { 
        (board 
            (rectangle 4 12)
            {
            (track "Track1" "11,W,N1,E,N1,W,N1,E,S1,W,S1,E" loop:True P1)
            (track "Track2" "36,E,S1,W,S1,E,S1,W,N1,E,N1,W" loop:True P2)
            }
        )
        (dice d:2 from:0 num:6)
        (piece "Marker" Each
        	(move 
        		(from
        			(from)
        			if:(if ("ActivatedPiece") 
        				True
        				("AllowToActivate")
        			)
        		)
        		(to
        			("SiteToMoveOnTrack")
        			if:(not (is Friend (who at:(to))))
        			(apply
        				(if (is Enemy (who at:(to)))
        					(remove (to))
        				)
        			)
        		)
                (then 
                	(if (= 1 (state at:(last To)))
                		("ActivatePiece")
                	)
                )
        	)
        )
        (map {(pair 0 4) (pair 1 5) (pair 2 2) (pair 3 3) (pair 4 1) (pair 5 5) (pair 6 6)})
    })  
    
    (rules 
            (start { 
                (place "Marker1" (sites Bottom) state:1)
                (place "Marker2" (sites Top) state:1)
            })
            
        (play 
        	(do (roll)
        		next:(forEach Piece)
        		(then
        			(if ("AllowToReplay") 
        				(moveAgain)
        			)
        		)
        	)
        )
        
        (end (if ("NoPiece" Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Issiren is a capturing game played in the Sahara. ")
        (aliases {"Ishigh√§n"})
        (rules "4x12 board. Twelve pieces per player. Six stick dice, green on one side and white on the other. The value of the throws equals the number of green faces, with the following exceptions: four green faces up allows a piece to be moved for the first time and a move of 1, plus another throw is granted; six white faces = 4 plus another throw; six green = 6 plus another throw.")
        (source "Bellin 1960: 21-22; Bellin 1963: 73-74")
        (version "1.2.3")
        (classification "board/race/reconstruction")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Colour "Die" state:1 fillColour:(colour Green))
    })
    
)
