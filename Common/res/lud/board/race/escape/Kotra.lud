(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"    
    (or 
        (and 
            (is Enemy (who at:(to))) 
            (= (topLevel at:(to)) 0)
        ) 
        (not (is Enemy (who at:(to))))
    )
)

(define "AllPiecesInFinalQuadrant"
    (= 0 (count Pieces Mover in:(difference (sites Board) (sites Mover "FinalQuadrant"))))
)

(define "EnterPieces"
    (if (is Occupied (handSite Mover))
        (forEach Die
            replayDouble:True 
            if:("DieNotUsed")
            (move 
                (from (handSite Mover))
                (to 
                    ("NextSiteFrom" (mapEntry "Entry" Mover) (- (pips) 1))
                    if:(is Empty (to))
                )
            )
            
            (then 
                (if (not (all DiceUsed))
                    (moveAgain)
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Kotra"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 6) 
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {11..0 12..23 End} P1 directed:True)
            (track "Track2" {12..23 11..0 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                replayDouble:True 
                if:("DieNotUsed")
                (if (= End ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:(and {
                                ("NotEnemyOrOnlyOne")
                                (if (not (is In (to) (sites Mover "StartingSide")))
                                    True
                                    (> 2 (size Stack at:(to)))
                                )
                                (if (not (is In (to) (sites Mover "AfterPenultimate")))
                                    True
                                    (>= 10 (count Pieces in:(sites {(handSite Mover) (mapEntry "Entry" Mover)})))
                                )
                            })
                            (apply 
                                (if (is Enemy (who at:(to)))
                                    (fromTo 
                                        (from (to))
                                        (to (handSite Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then 
                    (if (not (all DiceUsed))
                        (moveAgain)
                    )
                )
            )		
        )
        (hand Each)
        (regions "FinalQuadrant" P1 (sites {18..23}))
        (regions "FinalQuadrant" P2 (sites {0..5}))
        (regions "StartingSide" P1 (sites {0..11}))
        (regions "StartingSide" P2 (sites {12..23}))
        (regions "AfterPenultimate" P1 (sites {23}))
        (regions "AfterPenultimate" P2 (sites {0}))
        (map "Entry" {(pair P1 11) (pair P2 12)})
    })
    (rules 
        (start {
            (place Stack "Disc1" 11 count:15)
            (place Stack "Disc2" 12 count:15)
        })
        (play 
            (do (if (not "SameTurn") (roll))
                next:(or
                    ("EnterPieces")
                    (forEach Piece top:True)
                )
                (then
                    (if ("NoPiece" Mover) (set Score Mover 2))
                )
            )		
        )
        (end (if ("NoPiece" Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Kotra is a race game related to other European Tables games that was played in Iceland. References to the game appear in early texts, suggesting that the game came to Iceland at an early date.")
        (rules "2x12 board, with the spaces rendered as points, divided into half. Fifteen pieces per player, which begin on the rightmost point, with respect to the player, on the opposite side of the board from which they sit. Two six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other.  A throw of doubles forces the player to play the throw twice. More than two pieces of the same player may not occupy a point on the starting side of the board. The player cannot move past the penultimate point on the opposite side of the board until five pieces have been moved from the starting point. A single opponent's piece may be removed from the board when a player's piece lands on it, and the opponent's piece must reenter the board. Pieces cannot move to a point occupied by two opponent's piece, except when there are six opponent's pieces in a row, in which case the opponent's piece is removed from the board and must reenter. A piece cannot reenter then board on a point occupied by another piece, whether belonging to the player or the opponent, unless there are more captured pieces than points available, in which case the player may move to points with the opponent's pieces, removing them from the board to be reentered. When all of a player's pieces have reached the final quadrant of the board, the player may begin to remove them from the table. Removing all of the pieces from the board is the lowest-ranked method of winning; better wins are achieved by placing the pieces in a certain configuration before bearing off. These are valued as follows: Meistari (master): placing all the pieces on the final point, scoring 13 points; Stutti múkur (little monk): five pieces on each of the final three points, scoring seven points; Langi múkur (big monk): three pieces on the final five points, scoring five points; Langi hryggur (big back): three pieces on the first five points of the final quadrant, scoring three points. These count their full value if completed by using the values of both dice, or half if completed only using one die in a turn. Two points are added to the score if the opponent has a captured piece when the position is achieved. The player who bears off their pieces scores two points. When one player has borne off all of their pieces, the opponent is allowed five, seven, or thirteen extra throws to score, depending on how many points required to win. The game ends when it is clear the remaining player cannot achieve a scoring position. The game may also end immediately when the opponent has more captured pieces than they can enter, which scores the player fifteen points.")
        (source "Fiske 1905: 348-352.")
        (version "1.2.1")
        (classification "race/WishlistDLP")
        (credit "Eric Piette")
        (origin  "This game was played in Iceland, from around 1862 to 1888.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 0 Backgammon)
    })
    
)

