(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"    
    (or 
        (and {
            (is Enemy (who at:(to))) 
            (not (is In (to) (sites Mover "OppositeSide")))
            (= (topLevel at:(to)) 0)
        }) 
        (not (is Enemy (who at:(to))))
    )
)

(define "AllPiecesInFinalQuadrant"
    (= 0 (count Pieces Mover in:(difference (sites Board) (sites Mover "FinalQuadrant"))))
)

(define "SpecialWin" 
	    (if (is Mover P1)
	        (and {
	        	(= 15 (count Pieces Mover in:(sites {0..5})))
	            (is Friend (who at:3))
	            (is Friend (who at:4))
	            (is Friend (who at:5))
	            (= 3 (size Stack at:0))
	            (= 3 (size Stack at:1))
	            (= 3 (size Stack at:2))
	        })
	        (and {
	        	(= 15 (count Pieces Mover in:(sites {13..18})))
	            (is Friend (who at:16))
	            (is Friend (who at:17))
	            (is Friend (who at:18))
	            (= 3 (size Stack at:13))
	            (= 3 (size Stack at:14))
	            (= 3 (size Stack at:15))
	        })
	    )
	)

//------------------------------------------------------------------------------

(game "Schuster"
    (players 2)
    (equipment {
        (board (rectangle 2 13) 
            {
            (track "Track1" {6 12..7 5..0 13..18 20..25 End} P1 directed:True)
            (track "Track2" {19 25..20 18..13 0..5 7..12 End} P2 directed:True)
            } 
            use:Vertex
        ) 
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (= End ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NotEnemyOrOnlyOne")
                            (apply 
                                (if (is Enemy (who at:(to)))
                                    (fromTo 
                                        (from (to))
                                        (to (mapEntry "Bar" Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then 
                	(and
                    (if (not (all DiceUsed))
                        (moveAgain)
                    )
                    (if ("SpecialWin")
                    	(addScore Mover 2)
                    )
                    )
                )
            )		
        )
        (regions "FinalQuadrant" P1 (sites {20..25}))
        (regions "FinalQuadrant" P2 (sites {7..12}))
        (regions "OppositeSide" P1 (sites {25..20 18..13}))
        (regions "OppositeSide" P2 (sites {0..5 7..12}))
        (map "Bar" {(pair P1 6) (pair P2 19)})
    })
    (rules 
        (start { 
            (place Stack "Disc1" 12 count:15)
            (place Stack "Disc2" 25 count:15) 
        })
        (play 
            (do (if (not "SameTurn") (roll))
                next:(forEach Piece top:True)
            )		
        )
        
        (end {
            (if ("SpecialWin") (result Mover Win))
            (if ("NoPiece" Mover) (result Mover Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Schuster is a European Tables game played in Sweden by women. ")
        (rules "Similar to Grand Trictrac. Two dice. Played on a Backgammon board. ")
        (source "Fiske 1905: 318.")
        (version "1.2.1")
        (classification "board/race/escape/reconstruction")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style backgammon)
        (stackType Backgammon limit:3)
    })
    
)

