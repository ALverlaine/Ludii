(game "Ratio"
    (players {(player S) (player N)})
    <Game:equipment>
    (rules
        <Game:start>
        <Game:play>
        <Game:end>
    )
)

//------------------------------------------------------------------------------

(option "Game" <Game> args:{ <equipment> <start> <play> <end>}
    {
    (item "Game I" 
        <
        (equipment {
            (board
                (add
                    (remove
                        (merge 
                            {
                            (rectangle 3 3 diagonals:Alternating) 
                            (shift 0 2 (wedge 2)) 
                            (shift 0 -1 (rotate 180 (wedge 2))) 
                            }
                        )
                        vertices:{3 5 1}
                    )
                    edges:{{0 3} {1 5} {2 7} {0 1}}
                )
                use:Vertex
            )
            
            (piece "Foot" P1 
                (move Step Adjacent 
                    (to 
                        if:(and
                            (if (= 1 (count Turns)) (!= (centrePoint) (to)) True)
                            (is Empty (to))
                        )
                    )
                )
            )
            (piece "Gnome" P2 (move Step Forwards (to if:(is Empty (to)))))
            (hand P1)
            (regions "Home" P2 (intersection (sites Outer) (expand (sites Bottom))))
            (regions "Home" P1 (sites Top))
        })
        > 
        <
        (start {
            (place "Gnome2" (intersection (sites Outer) (expand (sites Bottom))))
            (place "Foot1" (handSite P1))
        })
        >
        <
        phases:{
        (phase "Opening"
            (play
                (move 
                    (from (handSite Mover))
                    (to (expand (sites Top)))
                    (then (moveAgain))
                )
            )
            (nextPhase "Main")
        )
        (phase "Main"
            (play (forEach Piece))
        )
        }
        >
        <
        (end {
            (if (no Moves P1) (result P2 Win))
            (if (is In (where "Foot" P1) (sites P2)) (result P1 Win))
        }) 
        >
        "The rules of the game I."
    )*  
    
    (item "Game II" 
            <
            (equipment {
                (board
                		(add
                        (remove
                            (merge 
                                {
                                (rectangle 3 3 diagonals:Alternating) 
                                (shift 0 2 (wedge 2)) 
                                (shift 0 -1 (rotate 180 (wedge 2))) 
                                }
                            )
                            vertices:{3 5}
                        )
                        edges:{{0 4} {2 6}}
                        )
                    use:Vertex
                )
                
                (piece "Foot" P1 (move Step Adjacent  (to if:(is Empty (to)))))
                (piece "Gnome" P2 (move Step Forwards (to if:(is Empty (to)))))
                (hand P1)
                (regions "Home" P2 (intersection (sites Outer) (expand (sites Bottom))))
                (regions "Home" P1 (sites Top))
            })
            > 
            <
            (start {
                (place "Gnome2" (intersection (sites Outer) (expand (sites Bottom))))
                (place "Foot1" (handSite P1))
            })
            >
            <
            phases:{
            (phase "Opening"
                (play
                    (move 
                        (from (handSite Mover))
                        (to (difference (sites Board) (intersection (sites Outer) (expand (sites Bottom)))))
                        (then (moveAgain))
                    )
                )
                (nextPhase "Main")
            )
            (phase "Main"
                (play (forEach Piece))
            )
            }
            >
            <
            (end {
                (if (no Moves P1) (result P2 Win))
                (if (is In (where "Foot" P1) (sites P2)) (result P1 Win))
            }) 
            >
            "The rules of the game II."
        ) 
    
    (item "Game III" 
            <
            (equipment {
                (board
                		(add
                        (remove
                            (merge 
                                {
                                (rectangle 3 3 diagonals:Alternating) 
                                (shift 0 2 (wedge 2)) 
                                (shift 0 -1 (rotate 180 (wedge 2))) 
                                }
                            )
                            vertices:{1}
                        )
                        edges:{{0 1} {3 9}}
                        )
                    use:Vertex
                )
                
                (piece "Foot" P1 (move Step Adjacent  (to if:(is Empty (to)))))
                (piece "Gnome" P2 (move Step Forwards (to if:(is Empty (to)))))
                (hand P1)
                (regions "Home" P2 (intersection (sites Outer) (expand (sites Bottom))))
                (regions "Home" P1 (sites Top))
            })
            > 
            <
            (start {
                (place "Gnome2" (intersection (sites Outer) (expand (sites Bottom))))
                (place "Foot1" (handSite P1))
            })
            >
            <
            phases:{
            (phase "Opening"
                (play
                    (move 
                        (from (handSite Mover))
                        (to (difference (sites Board) (intersection (sites Outer) (expand (sites Bottom)))))
                        (then (moveAgain))
                    )
                )
                (nextPhase "Main")
            )
            (phase "Main"
                (play (forEach Piece))
            )
            }
            >
            <
            (end {
                (if (no Moves P1) (result P2 Win))
                (if (is In (where "Foot" P1) (sites P2)) (result P1 Win))
            }) 
            >
            "The rules of the game III."
        )
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        }
    )
    
    (graphics {
        (player Colour P1 (colour White))
        (player Colour P2 (colour White))
    })
    
)
