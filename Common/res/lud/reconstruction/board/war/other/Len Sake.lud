(define "NumPieces" [#])

(define "DieNotUsed" (!= (pips) 0))

(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:(pips)
    )
)

//------------------------------------------------------------------------------

(game "Len Sake" 
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 6) 
                (shift 7 0 (rectangle 1 6))
                (shift 0 6 (rectangle 1 6))
                (shift 7 6 (rectangle 1 6))
            })
            {
            (track "Track1" {24 0..5 6..11 23..18 17..12} P1 directed:True)
            (track "Track2" {25 23..18 17..12 0..5 6..11} P2 directed:True)
            } 
            use:Vertex
        )
        (dice [#])
        (piece "Disc" Each
            (move
                (from (from) level:(level))
                (to ("NextSiteFrom" (from))
                    if:(or (not ("IsEnemyAt" (to))) (and ("IsEnemyAt" (to)) (= 1 (size Stack at:(to)))))
                    (apply 
                        (if ("IsEnemyAt" (to)) (remove (to)))	
                    )
                )
            )
        )
        (hand Each)
    })
    (rules
        (start {
            (place Stack "Disc1" (handSite P1) count:"NumPieces")
            (place Stack "Disc2" (handSite P2) count:"NumPieces")
        })
        phases:{
        (phase "Entering"
            (play
                (do 
                    (if ("NewTurn") (roll))
                    next:(forEach Die
                        if:("DieNotUsed")
                        (move
                            (from (handSite Mover))
                            (to ("NextSiteFrom" (handSite Mover)))
                            (then 
                                (if (not (all DiceUsed))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (is Empty (handSite Mover)) "Playing")
        )
        
        (phase "Playing" 
            (play
                (do 
                    (if ("NewTurn") (roll))
                    next:(forEach Die
                        if:("DieNotUsed")
                        (forEach Piece top:True)
                        (then 
                            (if (not (all DiceUsed))
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        }
        (end (if (> ("NumPieces") (count Pieces Next)) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Len Saké is a race game played in Thailand during the nineteenth century. ")
        (aliases {"Len Saké"})
        (rules "2x12 board, divided in half as in Backgammon. Played with dice. Pieces enter the board according to the throws of the dice into the left hand side of the board. All pieces must be entered onto the board before they may begin moving. Movement occurs in an anti-clockwise direction. Once they can move, an opponent's piece which is alone on a point can be taken when one of the player's pieces lands on that point. The first player to have a piece captured loses.
        ")
        (source "DLP evidence.")
        (id "991")
        (version "1.3.5")
        (classification "board/war/reconstruction")
        (credit "Eric Piette")
        (origin "This game was played in Thailand, around 1836.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table) // Temporary style until a more specific one exist for that game.
        (stackType 1 Count)
        (stackType 2 Count) 
        (stackType 0 Backgammon)
    })
    
)

