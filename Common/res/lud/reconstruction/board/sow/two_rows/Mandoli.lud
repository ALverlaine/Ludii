(define "PiecesOwnedBy" (count Cell at:(handSite #1)))

//------------------------------------------------------------------------------

(game "Mandoli" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            [(track "Track" "0,E,N,W" loop:True) | (track "Track" "5,W,N,E" loop:True) ] // Unknown which direction the sowing occurs.
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        (start (set Count 6 to:(sites Track)))
        
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(< 0 (count at:(from)))
                )
                (then 
                    (sow 
                        if:(and 
                            (is In (to) (sites Next)) 
                            (is Even (count at:(to))) 
                        ) 
                        apply:(fromTo 
                            (from (to)) 
                            (to (handSite (mover))) 
                            count:(count at:(to))
                        )
                        backtracking:True
                    )
                )
            )		
        ) 
        
        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Mandoli is a two-row mancala-style game board documented on the island of Hydra in Greece in the early nineteenth century.")
        (rules "2x6 board. Six counters in each hole. Unknown which direction the sowing occurs. Captures cannot be made on the first move. Captures are made when the final counter lands in a hole making it contain an even number. If the hole behind the captured hole also contains an even number, these are captured. Play continues until a player cannot move. The player who has captured the most counters wins.")
        (source "Galt 1813: 241-242.")
        (id "430")
        (version "1.3.6")
        (classification "board/sow/two rows/reconstruction")
        (credit "Eric Piette")
        (origin "This game was played in Greece, around 1810.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)

