(define "CaptureEnemyToPiece"
    (apply 
        (if (is Enemy (who at:(to)))
            (remove (to))
        )
    ) 
)

(define "CaptureForwardDiagonal"
	    (move
	        Step 
	        (directions {FR FL}) 
	        (to 
	            if:(is Enemy (who at:(to))) 
	            (apply (remove (to)))
	        )
	    )
	)

(define "DoubleStep"
	    (move
	        Slide 
	        Forward 
	        (between (exact 2) if:(is Empty (between))) 
	        (to if:(is Empty (to))) 
	    )
	)

(define "RememberMoved" (set State at:#1 1))
(define "MovedBefore" (= 1 (state at:#1)))

(define "SpecialStartingPawns" (sites {"A2" "D2" "H2" "A7" "H7" "E7"}))

//------------------------------------------------------------------------------

(game "Welschschach"
	    (players {(player N) (player S)})  
    (equipment {
        (board (square 8) )
        (piece "King_noCross"
        		(move Step
                        (to if:(not (is Friend (who at:(to))))
                            ("CaptureEnemyToPiece")
                        )
                    )			
        )
        (piece "Queen"
        		(or
	        		(if (not ("MovedBefore" (from)))
	        			(move Hop Diagonal 
	        				(between (exact 2) if:True) 
	        				 (to if:(not (is Friend (who at:(to))))
	 	                            ("CaptureEnemyToPiece")
	 	                        )
	        			)
	        		)
	        		(move Step
	        				Diagonal
	                        (to if:(not (is Friend (who at:(to))))
	                            ("CaptureEnemyToPiece")
	                        )
	                    )	
	        		(then ("RememberMoved" (last To)))
        		)
        )
        (piece "Bishop_noCross"
        		(move Hop Diagonal 
        				(between if:True) 
        				 (to if:(not (is Friend (who at:(to))))
 	                            ("CaptureEnemyToPiece")
 	                        )
        			)		
        )
        (piece "Knight"
        		(move
                        Leap 
                        "KnightWalk" 
                        (to 
                            if:(not (is Friend (who at:(to)))) 
                            "CaptureEnemyToPiece"
                        ) 
                    )		
        )
        (piece "Rook"
        		(move
                        Slide 
                        Orthogonal
                        (to 
                            if:(is Enemy (who at:(to))) 
                            "CaptureEnemyToPiece"
                        ) 
                    )		
        )
        (piece "Pawn"
                (or {
                		 (if (is In (from) (intersection ("SpecialStartingPawns") (sites Start (piece (what at:(from))))))
                             "DoubleStep"
                         )
                    "StepForwardToEmpty" 
                    "CaptureForwardDiagonal"
                })
        ) 
    })
    (rules 
        (start {   
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Bishop_noCross1" {"C1" "F1"}) (place "Queen1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Bishop_noCross2" {"C8" "F8"}) (place "Queen2" coord:"E8") (place "King_noCross2" coord:"D8") 
        })
        (play (forEach Piece))
        (end (if (no Moves Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Welschschach is a German Chess game played in the village of Str√∂beck, and played by people who traveled to the village because of its reputation for Chess games.")
        (rules "8x8. Each player begins with a complement of pieces, each with their own powers of movement as follows: King (x1): may move one space in any direction; Queen (x1): moves one space diagonally, may leap two spaces diagonally on its first move over any intervening pieces; Bishop (x2): moves diagonally two spaces, jumping over any intervening pieces; Knight (x2): moves orthogonally one space and then diagonally another space, jumping over any intervening pieces; Rook (x2): moves orthogonally any distance; Pawns (x8): move forward one space or diagonally forward one space to capture. Only the Rooks' and Queens' pawns may move forward two spaces for their first move. When a Pawn reaches the row on the opposite side of the board from which it began, it must move two spaces backward to the sixth row, then to the fourth, and then to the second, and then is promoted to Queen. It cannot jump over any pieces or taken a piece during these moves. A Pawn is immune from capture on the eight rank, but not on the others while making these promotion moves. Each player must move the Rooks' Pawns and Queens' Pawn in this way, as well as the jump of the Queen, in their first four moves. There is no castling. A piece is captured when an opponent's piece moves onto its space. The King is in check when it can be taken on the opponent's next turn; it must not remain in check at the end of the player's turn. When the king cannot move out of check, it is checkmated and the opponent wins.")
        (source "Murray 1913: 392.")
        (version "1.2.1")
        (classification "war/chaturanga/WishlistDLP")
        (origin  "This game was played in Germany, around 1661.")
        }
    )
    
    (graphics {
        (piece Scale "Pawn" 0.825)
        (show Check "King")
        (board Style Chess)
    })
    
)

