(define "ThrowValue" (mapEntry (count Pips))) 

(define "SiteToMoveOnTrack" (trackSite Move #1 steps:#2))

(define "SpecialThrows" 
	    (is In ("ThrowValue") (sites {1 4 6}))
	)

(define "Tab" (= 1 ("ThrowValue")))

(define "CaptureEnemyPiece" 
	    (apply 
	        if:(is Enemy (who at:(to))) 
	        (remove (to))
	    ) 
	)

(define "Move"
	            (move 
	                (from)
	                (to 
	                    ("SiteToMoveOnTrack" "HomeTrack" ("ThrowValue")) 
	                    "CaptureEnemyPiece"
	                )
	            )
	)

//------------------------------------------------------------------------------

(game "Tab"
    (players 2)
    (equipment {
        (board 
        	(rectangle 4 <Board:size>) 
            {
            (track "HomeTrack1" "0,E,N1,W" P1 directed:True)
            (track "HomeTrack2" "27,W,S1,E" P2 directed:True)
            (track "MiddleTrack" "13,W,N1,E" loop:True)
            (track "EnemyTrack1" "14,E,N1,W,S1,E" P1 directed:True)
            (track "EnemyTrack2" "13,W,S1,E,N1,W" P2 directed:True)
            }
        	use:Vertex
        )
        (piece "Stick" Each ("Move"))
        (regions "AllSites" (sites Board))
        (dice d:2 from:0 num:4)
        (map "Throw" {(pair 0 6) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4)})
    })
    (rules 
        (start {  
            (place "Stick1" (sites Bottom))
            (place "Stick2" (sites Top))
        })
        	phases:{
        	(phase "InitGame" 
        		(play
        			(do 
		        		(roll) 
		        		next:(move Pass)
		        		(then 
		                        (if ("Tab")
		                            (moveAgain)
		                        )
		                    )
		        	)
        		)
        		(nextPhase ("Tab") "Play")
        	)
        	(phase "Play"
        		(play
		        	(do 
		        		(roll) 
		        		next:(forEach Piece)
		        		(then 
		                        (if ("SpecialThrows")
		                            (moveAgain)
		                        )
		                    )
		        	)
	        	)
        	)
        }
        (end (if (no Moves Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Board size" <Board> args:{ <size> }
    {
    (item "7"       <7> "Each row has 7 holes.")   
    (item "9"       <9> "Each row has 9 holes.")   
    (item "11"      <11> "Each row has 11 holes.")   
    (item "13"      <13> "Each row has 13 holes.")   
    (item "15"      <15> "Each row has 15 holes.")   
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tab is a capturing game that is commonly played in North Africa and Southwest Asia, and in other places which have been in contact wit hthem, such as Comoros. It is typically played in holes in the sand, and is at least several hundred years old. Tab boards are found as graffiti on many monuments throughout the region in which it is played.")
        (aliases {"Tab Wa Dukk"})
        (rules "4x7-15 (odd number only) board. One piece in each holes in the outer row. Four palm branches used as dice, with one side white and the other side yellow. The throws are equal to the number of white sides that fall up; when only yellow sides are up, the score is 6. When a player throws 1, 4, or 6, the player throws again. Players take turns throwing, until one throws 1, and that player begins to play. Play must begin with their rightmost piece. Each player moves in a boustrophedon path, from left to right in the row closest to them, right to left in the second row, and left to right in the third row. From there, the player may move again into the second row and continue as before, or move into the fourth row, proceeding from right to left, as long as at least one of the opponent's pieces remains there. The piece may enter the third row again upon reaching the end of the fourth row, but only when the player has either no pieces in their first row, or one group of pieces in the same spot (see below). When a piece has moved out of the fourth row, it may not enter it again during the game. When a player's piece lands in the same spot as another piece belonging to the player, the pieces may move as one piece. Upon throwing a 1, the player may use that 1 to separate the pieces again. If a move brings such a group into a row in which they have already passed through, they become single pieces again. When a player's piece lands on a space occupied by an opponent's, piece, the opponent's piece is captured. The player who captures all of the opponent's pieces wins.")
        (source "Lane 1836: 346-349.")
        (version "1.2.0")
        (classification "war/WishlistDLP")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (show Edges Hidden)    
        (show Symbol "disc" "AllSites" Vertex fillColour:(colour White) edgeColour:(colour Black) scale:1)
        (piece Colour "Die" state:0 fillColour:(colour Yellow))
        (piece Scale "Stick" 0.5)
        
    })
    
)

