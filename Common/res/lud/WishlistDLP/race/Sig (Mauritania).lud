(define "ThrowValue" (count Pips))

(define "SiteToMoveOnTrack" (trackSite Move #1 steps:#2))

(define "IsSpecialThrow" (= ("ThrowValue") 4))

(define "ActivatedThrow" (is In ("ThrowValue") (sites {1 8})))

(define "ActivatedPiece" (< 0 (state at:#1)))

(define "ActivePiece" (set State at:#1 1))

(define "MiddleSites" (difference (sites Board) (union (sites Top) (sites Bottom))))

(define "CaptureMove"
	(if (is Enemy (who at:(to)))
		(remove (to))
	)
)

(define "NeverGoInEnemyHome"
	(< (state at:#1) 2)
)

(define "MoveOnLoop" 
	(= (state at:#1) 3)
)

(define "MoveBackOnMiddleSites" 
	(= (state at:#1) 2)
)

(define "Move"
	(if ("NeverGoInEnemyHome" (from))
		(or
			(if (<= (var) 0)
				(move
					(from (from)
						if:(if
								("ActivatedPiece" (from))
								True
								("ActivatedThrow")
							)
					)
					(to 
						("SiteToMoveOnTrack" "Track" ("ThrowValue"))	
						if:(if (is In (to) ("MiddleSites"))
							(not (is Friend (who at:(to))))
							(is Empty (to))
						   )
						(apply ("CaptureMove"))
					)
					(then
						(if (not ("ActivatedPiece" (last To)))
							("ActivePiece" (last To) )
						)
					)
				)
			)
			(if (or (= ("ThrowValue") 8) (> (var) 0))
				(move
						(from (from))
						(to 
							("SiteToMoveOnTrack" "Track" 1)	
							if:(if (is In (to) ("MiddleSites"))
								(not (is Friend (who at:(to))))
								(is Empty (to))
							   )
							(apply ("CaptureMove"))
						)
						(then
							(and
								(if (not ("ActivatedPiece" (last To)))
									("ActivePiece" (last To) )
								)
								(if (> (var) 0)
									(and
										(if (> (var) 1)
											(moveAgain)
										)
										(set Var (- (var) 1))
									)
									(and
										(set Var (- ("ThrowValue") 1))
										(moveAgain)
									)
								)
							)
						)
					)
			)
			(then
					(if (is In (last To) (if (is In (mover) (players Team1)) (sites Top) (sites Bottom)))
						(set State at:(last To) 2)
					)
				)
		)
		(if ("MoveBackOnMiddleSites" (from))
			(move
					(from (from))
					(to 
						("SiteToMoveOnTrack" "GoBackTrack" ("ThrowValue"))	
						if:(if (is In (to) ("MiddleSites"))
							(not (is Friend (who at:(to))))
							(is Empty (to))
						   )
						(apply ("CaptureMove"))
					)
					(then
						(if (is In (last To) ("MiddleSites"))
							(set State at:(last To) 3)
						)
					)
				)
			(if ("MoveOnLoop" (from))
				(move
						(from (from))
						(to 
							("SiteToMoveOnTrack" "LoopTrack" ("ThrowValue"))	
							if:(if (is In (to) ("MiddleSites"))
								(not (is Friend (who at:(to))))
								(is Empty (to))
							   )
							(apply ("CaptureMove"))
						)
					)
			)
		)
	)
)

//------------------------------------------------------------------------------

(game "Sig (Mauritania)"
    (players <Players:num>)
    (equipment {
        (board 
            (rectangle 4 <Board:size>)
            {
            (track "Track1" "19,W,N1,E,N1,W,N1,E" P1 directed:True)
            (track "Track2" "79,W,S1,E,S1,W,S1,E" P2 directed:True)
            (track "LoopTrack1" "40,E,S1,W" loop:True P1)
            (track "LoopTrack2" "20,E,N1,W" loop:True P2)
            (track "GoBackTrack1" "79,W,S1,E,S1,W" P1 directed:True)
            (track "GoBackTrack2" "19,W,N1,E,N1,W" P2 directed:True)
            }
            use:Vertex
        )
        (dice d:2 from:0 num:8)
        (piece "Marker" P1 ("Move"))
        (piece "Stick" P2 ("Move"))
    })
    (rules 
    	(start 
    		{
    		(place "Marker1" (sites Bottom))
    		(place "Stick2" (sites Top))
    		<Players:start>
    		}
    	)
        (play
        	(do (if (<= (var) 0) (roll))
            next:(if (is In (mover) (players Team1))
	            	(forEach Piece P1)
	            	(forEach Piece P2)
            	)
            (then
                    (if ("IsSpecialThrow")
                        (moveAgain)
                    )
                )
            )
        )	
        (end {
        		(if ("NoPiece" P1) (result Team2 Win))
        		(if ("NoPiece" P2) (result Team1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(option "Players" <Players> args:{ <num> <start>} {
    (item "2"    <2>   
    <
    (set Team 1 {P1})
    (set Team 2 {P2})
    >
    "The game involves 2 players.")   
    (item "4"   <4>   
    	    <
    	    (set Team 1 {P1 P3})
    	    (set Team 2 {P2 P4})
    	    >		
     "The game involves 4 players.")*   
    (item "6"    <6>   
    <
    (set Team 1 {P1 P3 P5})
    (set Team 2 {P2 P4 P6})
    >		
    "The game involves 6 players.")   
    (item "8"    <8>   
    <
    (set Team 1 {P1 P3 P5 P7})
    (set Team 2 {P2 P4 P6 P8})
    >	
    "The game involves 8 players.")   
    (item "10"    <10>   
    <
    (set Team 1 {P1 P3 P5 P7 P9})
    (set Team 2 {P2 P4 P6 P8 P10})
    >	
    "The game involves 10 players.")   
    (item "12"    <12>   
    <
    (set Team 1 {P1 P3 P5 P7 P9 P11})
    (set Team 2 {P2 P4 P6 P8 P10 P12})
    >	
    "The game involves 12 players.")   
})

(option "Board Size" <Board> args:{ <size> } {
    (item "2"    <2>   "Each row has 2 Holes.")   
    (item "3"    <3>   "Each row has 3 Holes.")   
    (item "4"    <4>   "Each row has 4 Holes.")   
    (item "5"    <5>   "Each row has 5 Holes.")   
    (item "6"    <6>   "Each row has 6 Holes.")   
    (item "7"    <7>   "Each row has 7 Holes.")   
    (item "8"    <8>   "Each row has 8 Holes.")   
    (item "9"    <9>   "Each row has 9 Holes.")   
    (item "10"    <10>   "Each row has 10 Holes.")   
    (item "11"    <11>   "Each row has 11 Holes.")   
    (item "12"    <12>   "Each row has 12 Holes.")   
    (item "13"    <13>   "Each row has 13 Holes.")   
    (item "14"    <14>   "Each row has 14 Holes.")   
    (item "15"    <15>   "Each row has 15 Holes.")   
    (item "16"    <16>   "Each row has 16 Holes.")   
    (item "17"    <17>   "Each row has 17 Holes.")   
    (item "18"    <18>   "Each row has 18 Holes.")   
    (item "19"    <19>   "Each row has 19 Holes.")   
    (item "20"    <20>   "Each row has 20 Holes.")*   
    (item "21"    <21>   "Each row has 21 Holes.")   
    (item "22"    <22>   "Each row has 22 Holes.")   
    (item "23"    <23>   "Each row has 23 Holes.")   
    (item "24"    <24>   "Each row has 24 Holes.")   
    (item "25"    <25>   "Each row has 25 Holes.")   
    (item "26"    <26>   "Each row has 26 Holes.")   
    (item "27"    <27>   "Each row has 27 Holes.")   
    (item "28"    <28>   "Each row has 28 Holes.")   
    (item "29"    <29>   "Each row has 29 Holes.")   
    (item "30"    <30>   "Each row has 30 Holes.")   
    (item "31"    <31>   "Each row has 31 Holes.")   
    (item "32"    <32>   "Each row has 32 Holes.")   
    (item "33"    <33>   "Each row has 33 Holes.")   
    (item "34"    <34>   "Each row has 34 Holes.")   
    (item "35"    <35>   "Each row has 35 Holes.")   
    (item "36"    <36>   "Each row has 36 Holes.")   
    (item "37"    <37>   "Each row has 37 Holes.")   
    (item "38"    <38>   "Each row has 38 Holes.")   
    (item "39"    <39>   "Each row has 39 Holes.")   
    (item "40"    <40>   "Each row has 40 Holes.")   
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sig is a word used for many games throughout Saharan Africa. This board game is a race game played in the Tagant region of Mauritania. The game is played in the sand, with the central two rows elevated on an artificial dune. ")
        (rules "The board consists of four rows of any number of holes. Two to twelve players, who play on two equal teams. The number of pieces per team is equal to the number of holes in one row. One team plays as pieces of camel dung, the other as sticks. The pieces begin, one in each hole, in the outer rows of the board. Eight sticks, used as dice, with a marked side and a blank side. The values of the throws are equal to the number of marked sides which land face up. A throw of 4 grants the player another throw. Pieces can only be moved for the first time with a throw of 1 or 8. One piece may move one space on a throw of 1, or eight pieces may each move one space on a throw of 8. Once a piece has made its initial move, it may move according to the throws of the sticks. The bottom team's pieces move from right to left in the home row, left to right in the next row, right to left in the third row, and then into the opponent's home row. The top team moves left to right (from their perspective) in their home row, right to left in the next row, left to right in the next row, and into the bottom player's home row. When a team's piece lands on a space in the central two rows occupied by an opponent's piece, the opponent's piece is captured. Pieces cannot be captured in the home rows. When a piece moves into the opponent's home row, they move back into the central two rows, moving in the opposite direction as before. After moving into the home row a piece can no longer be captured, even when it is in the central two rows. Play continues until both team's pieces have moved out of their home row and can no longer be captured. Players then alternate turns throwing the sticks, removing one of the opponent's pieces every time a 4 is thrown. A team wins when only their pieces are left on the board.")
        (source "BÃ©art 1955: 429-430.")
        (version "1.2.1")
        (classification "race/WishlistDLP")
        (credit "Eric Piette")
        (origin  "This game was played in Mauritania, around 1955.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (show Symbol "disc" Vertex (sites Board) fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
        (piece Scale "Marker" 0.5)
        (piece Scale "Stick" 0.5)
        (piece Colour "Die" state:1 fillColour:(colour Black))
    })
    
)
