(define "Columns" 8)
(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )
(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2)) ) )

//------------------------------------------------------------------------------

(game "Mweso"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "16,E,N1,W" loop:True P2)
            }
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Track "TrackCCW1"))                              // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))                              // P2 home
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top) ))   // P2 inner Home
        (hand Each)
    })  
    (rules 
        
        (start {
            (set Count 32 at:(handSite P1) ) 
            (set Count 32 at:(handSite P2) ) 
        })
        
        phases:{
        (phase "Placing"
            (play
                (move
                    (from (handSite Mover))
                    (to (sites Mover "Home"))
                    count:1
                )
            )
            (nextPhase Mover (is Empty (handSite Mover)) "Sowing")
        )
        
        (phase "Sowing"
            (play
                (move Select
                    (from 
                        (if (is Mover Prev)
                            (sites {(var "Replay")})
                            (sites Mover) 
                        )
                        if:(is Occupied (from))
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (< 1 (count at:(to)))
                                (and {
                                    (moveAgain)
                                    (set Var "Replay" (to))
                                    (if (is In (to) (sites Mover "Inner"))
                                        (if 
                                            (and
                                                (is Occupied ("OppositePit"))
                                                (is Occupied ("OppositeOuterPit"))
                                            )	
                                            (and {
                                                (fromTo
                                                    (from ("OppositePit"))
                                                    (to (last From))
                                                    count:(count at:("OppositePit"))
                                                )
                                                (fromTo
                                                    (from ("OppositeOuterPit"))
                                                    (to (last From))
                                                    count:(count at:("OppositeOuterPit"))
                                                )
                                                (sow 
                                                    (last From)
                                                    count:(+ (count at:("OppositePit")) (count at:("OppositeOuterPit")))
                                                    "TrackCCW"
                                                    owner:(mover)
                                                )
                                            })
                                        )
                                    )
                                })
                            )
                        )
                    )
                )
            )
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mweso is aa four-row mancala-style game board popular in Uganda. It has been played there since at least the early twentieth century, but it is likely older.")
        (aliases {"Omweso" "Okusora" "Akileyisit"})
        (rules "4x8 board. 32 counters per player. Players may arrange the counters however they like in their holes. Players sow by taking the counters from one of their holes and dropping them one by one in an anti-clockwise direction only in the two rows closest to them. When the last counter lands in an empty hole, the turn is over. When the last counter falls into an occupied hole, the contents of this hole are picked up and sowing continues. If the final counter falls into an occupied hole in the inner row, and the two holes opposite it in the opponent's rows are occupied, the counters in these two rows of the opponent are taken. They are then sown on the player's side of the board beginning from the hole that received the first counter of the sowing that led to the capture. Both of the opponent's holes must be occupied to capture. Clockwise sowing is allowed in certain circumstances. If a clockwise move from the four holes on the left hand side of the players board (i.e., the left two holes in the outer row or the left two hole in the inner row) can lead to a capture, a clockwise move is allowed. When capturing, the captured seeds made from such a move may also be sown in a clockwise direction if they lead to a capture, otherwise they must be sown in the normal direction. Sowing cannot occur with single counters. The player who can no longer move loses. A player may also win by capturing the counters at both ends of the opponent's rows in the same move.")
        (source "Shackell 1934: 14-19.")
        (version "1.2.7")
        (classification "sow/four rows/WishlistDLP")
        (credit "Eric Piette")
        (origin  "This game was played in Uganda, around 1912.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)

