(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "NextHoleFrom" (trackSite Move from:#1 #2 #3))

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

(define "OwnedHoles" 
    (if (is Mover P1)
        (sites (values Remembered "OwnedP1"))
        (sites (values Remembered "OwnedP2"))
    )
)

(define "CaptureMove"
    (if (is Occupied ("NextHoleFrom" (to) #1 steps:1))
        (and (moveAgain) #2)
        (and
            (if (is Occupied ("NextHoleFrom" (to) #1 steps:2))
                (and
                    (fromTo
                        (from ("NextHoleFrom" (to) #1 steps:2))
                        (to (mapEntry Mover))
                        count:(count at:("NextHoleFrom" (to) #1 steps:2))
                    )
                    (if (and (is Empty ("NextHoleFrom" (to) #1 steps:3)) (is Occupied ("NextHoleFrom" (to) #1 steps:4)))
                        (and
                            (fromTo
                                (from ("NextHoleFrom" (to) #1 steps:4))
                                (to (mapEntry Mover))
                                count:(count at:("NextHoleFrom" (to) #1 steps:4))
                            )
                            (if (and (is Empty ("NextHoleFrom" (to) #1 steps:5)) (is Occupied ("NextHoleFrom" (to) #1 steps:6)))
                                (and
                                    (fromTo
                                        (from ("NextHoleFrom" (to) #1 steps:6))
                                        (to (mapEntry Mover))
                                        count:(count at:("NextHoleFrom" (to) #1 steps:6))
                                    )
                                    (if (and (is Empty ("NextHoleFrom" (to) #1 steps:7)) (is Occupied ("NextHoleFrom" (to) #1 steps:8)))
                                        (fromTo
                                            (from ("NextHoleFrom" (to) #1 steps:8))
                                            (to (mapEntry Mover))
                                            count:(count at:("NextHoleFrom" (to) #1 steps:8))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (set Var "Direction" 0)
        )
    )		
)

//------------------------------------------------------------------------------

(game "Bay Khom"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 4
            {
            (track "TrackCW" "0,5,E,9,4,W" loop:True)
            (track "TrackCCW" "0,1,E,9,8,W" loop:True)
            }
        )
        (piece "Seed" Shared) 
        (map {(pair P1 LastSite) (pair P2 FirstSite)}) // kalahs (storage pits)
        (regions P1 (sites Bottom))                    // P1 home
        (regions P2 (sites Top))                       // P2 home 
        (hand Each)
    })  
    (rules 
        
        (start { 
            (set Count 4 to:(union (sites P1)  (sites P2)) ) 
            (set Count 5 at:(mapEntry 1) ) 
            (set Count 5 at:(mapEntry 2) )
            (set RememberValue "OwnedP1" (sites Bottom))
            (set RememberValue "OwnedP2" (sites Top))
        })
        
        (play 
            (or
                (if (or (<= (var "Direction") 0) (= (var "Direction") 1))
                    (move Select
                        (from 
                            (if (is Mover Prev)
                                ("PlayFromLastHole") 
                                ("OwnedHoles")
                            )
                            if:(is Occupied (from))
                        )
                        (then
                            (sow
                                "TrackCW"
                                apply:("CaptureMove" "TrackCW" (set Var "Direction" 1))
                            )
                        )
                    )
                )
                (if (or (<= (var "Direction") 0) (= (var "Direction") 2))
                    (move Select
                        (from 
                            (if (is Mover Prev)
                                ("PlayFromLastHole") 
                                ("OwnedHoles")
                            )
                            if:(is Occupied (from))
                        )
                        (then
                            (sow
                                "TrackCCW"
                                apply:("CaptureMove" "TrackCCW" (set Var "Direction" 2))
                            )
                        )
                    )
                )
            )
        )
        
        (end
            (if  (no Moves Next)
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Bày Khòm is a two-row mancala-style board game from Cambodia. It was observed in the mid-twentieth century, but it is possible that Khmer texts from the middle ages mention it as \"khbal camuon.\"")
        (aliases {"Bày Khòm"})
        (rules "2x4 board with two stores on either end, the store on a player's right belonging to them. Four counters per hole, five in each store. Players sow in the direction of their choosing from one of the holes in their row, not from the stores, on each turn, sowing also into the stores. If the next hole has counters, these are picked up and sowing continues with these counters. When this next hole is empty, the counters in the next hole after that are captured and the turn ends. If there are a series of alternating empty holes and holes with counters, the counters in these holes are captured until two empty or full holes are reached. When the point of the game is reached that play cannot continue, the players claim the counters in their holes, and a new game begins. The players refill their holes, beginning with their store, which receives five counters, and four in each other. Holes are only filled with exactly four counters, any extra are set aside. If a player cannot fill any of their holes with counters, these holes are eliminated from play. Play continues in this way until one player captures all of the counters or the other player cannot fill any holes.")
        (source "Porée-Maspero 1962: 65-66.")
        (version "1.2.3")
        (classification "sow/two rows/WishlistDLP")
        (credit "Eric Piette")
        (origin  "This game was played in Cambodia, from around 1940 to 1945.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
