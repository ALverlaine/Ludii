(define "NextSiteFrom" ("NextSiteOnTrack" #2 from:#1))

(define "EnterPieces"
    (if (is Occupied (mapEntry "Bar" Mover))
        (forEach Die
            if:("DieNotUsed")
            (move 
                (from (mapEntry "Bar" Mover))
                (to 
                    ("NextSiteFrom" (mapEntry "Bar" Mover) (pips))
                    if:(< (size Stack at:(to)) 2)
                    ("HittingCapture" (mapEntry "Bar" Next))
                )
            )
            (then ("ReplayNotAllDiceUsed"))
        )
    )
)

(define "AllPiecesInFinalQuadrant" ("AllOwnedPiecesIn" (sites Mover "FinalQuadrant")))

//------------------------------------------------------------------------------

(game "NardExhibition"
    (players 2)
    (equipment {
        ("BackgammonBoard" ("BackgammonTracksWithBar" End)) 
        (dice d:6 num:2)
        (map "Bar" {(pair P1 6) (pair P2 19)})
        (regions "FinalQuadrant" P1 (sites {20..25}))
        (regions "FinalQuadrant" P2 (sites {7..12}))
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if ("IsEndTrack" ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NoEnemyOrOnlyOne" (to))
                            ("HittingCapture" (mapEntry "Bar" Next))
                        )
                    )
                )
                (then ("ReplayNotAllDiceUsed"))
            )
        )
    })
    (rules 
        (start {
            (place Stack "Disc1" 7 count:5) (place Stack "Disc1" 4 count:3) 
            (place Stack "Disc1" 13 count:5) (place Stack "Disc1" 25 count:2)
            (place Stack "Disc2" 20 count:5) (place Stack "Disc2" 17 count:3) 
            (place Stack "Disc2" 0 count:5) (place Stack "Disc2" 12 count:2)
        })
        (play 
            ("RollEachNewTurnMove"
                (priority {
                    ("EnterPieces")
                    (if (is Empty (mapEntry "Bar" Mover)) (max Distance (forEach Piece top:True)))
                })
                (then
                    (if (no Pieces Mover)
                        (if (no Pieces Enemy in:(sites Mover "FinalQuadrant")) (addScore Mover 1) (addScore Mover 2))
                    )
                )
            )
        )
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Nard is a race game from Persia, documented since at least 600 CE. It is thought to be an ancestor to Backgammon and other games, like Japanese Ban-Sugoroku.")
        (rules "")
        (source "Himly 1879: 679-681.")
        (id "149")
        (version "1.3.7")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Persia, around 1879.")
        }
    )
    
    (graphics {
        (board Style backgammon)
        (stackType Backgammon)
    })
    
)
