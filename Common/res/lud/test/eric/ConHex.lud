(define "CellOfLastVertex" 
    (sites Incident Cell of:Vertex at:(last To)) 
)

(define "AllHolesOfCell"
    (intersection
        (sites Incident Vertex of:Cell at:(site))
        (sites "Holes")
    )
)

(define "NumHolesCells" 
    (count Sites in:("AllHolesOfCell"))
)

(define "NumOwnedHolesCells"
    (count Sites 
        in:(intersection
            ("AllHolesOfCell")
            (sites Occupied by:Mover on:Vertex)
        )
    )		
)

(define "MajorityOfEmptyCell" 
    (if (is In (site) (sites Centre))
        (>=
            (+ ("NumOwnedHolesCells") (if (= (mover) (who Vertex at:(centrePoint Vertex))) 1 0))
            3
        )
        (>=
            ("NumOwnedHolesCells")
            (if (is Even ("NumHolesCells")) (/ ("NumHolesCells") 2) (+ (/ ("NumHolesCells") 2) 1))
        )
    )
)

(define "CaptureTheCell" 
    (claim
        (piece (id "Rectangle" Mover))
        (to
            Cell 
            (site) 
        )
    ) 
)

//------------------------------------------------------------------------------

(game "ConHex" 
    (players 2)
    (equipment {
        
        (board
            (add
                (keep 
                    (rotate 45 (dual (square 12 diagonals:Concentric)))
                    (poly { {2.5 2.5} {2.5 9.5} {9.5 9.5} {9.5 2.5} })
                )
                vertices:{{6 6}}
            )
        )
        (piece "Marker" Each)
        (piece "Rectangle" Each)
        (regions "Holes" 
            (sites { 
                99 59 40 0
                51 41 61 69 77 83 89 93 94 90 86 80 74 66 58 48 38 30 22 16 10 6 5 9 13 19 25 33 29
                42 52 62 70 78 84 85 79 73 65 57 47 37 39 21 15 14 20 26 34
                35 43 53 63 71 72 64 56 46 36 28 27
                44 45 54 55 100
                
            })
        )
        (regions P1 { (sites Side N) (sites Side S) } )
        (regions P2 { (sites Side W) (sites Side E) } )
    })
    (rules
        
        (play 
            (move Add (to Vertex (sites Empty Vertex) if:(is In (to) (sites "Holes")))
                (then 
                    (forEach Site
                        "CellOfLastVertex" 
                        (if "MajorityOfEmptyCell"
                            "CaptureTheCell"
                        )
                        (then
                            (forEach Site (intersection (sites Occupied by:Mover) ("CellOfLastVertex"))
                                (if (is Connected at:(site) Mover)
                                    (trigger "Connected" Mover)
                                )
                            )		
                        )
                    )
                )		
            )
        )
        
        (end {
            (if (is Triggered "Connected" Mover) (result Mover Win))
            (if (<= 52 (count Moves)) (result Mover Draw))
        }) 
    )
)
//------------------------------------------------------------------------------

(metadata 
    (info
        {
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        (show Symbol "disc" "Holes" Vertex fillColour:(colour White) edgeColour:(colour Black) scale:0.5)
        (player Colour P1 (colour Blue))
        (player Colour P2 (colour Red))
        (board Background image:"triangle.svg" fillColour:(colour Red) edgeColour:(colour Red) scale:1.2 rotation:90 offsetX:-0.1)
        (board Background image:"triangle.svg" fillColour:(colour Red) edgeColour:(colour Red) scale:1.2 rotation:270 offsetX:0.1)
        (board Background image:"triangle.svg" fillColour:(colour Blue) edgeColour:(colour Blue) scale:1.18 offsetY:0.12)
        (board Background image:"triangle.svg" fillColour:(colour Blue) edgeColour:(colour Blue) scale:1.18 rotation:180 offsetY:-0.12)
        (piece Scale "Marker" 0.5)
    })
    
)
