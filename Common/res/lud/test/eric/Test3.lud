(game "Ashanti Alignment Game" 
    (players 2)
    
    (equipment { 
        (board 
            (rectangle 5 6)
        use:Vertex)
        
        (hand Each)
        
        (piece "Marker" Each 
            (move Step 
                (to if:
                    (is Empty 
                        (to)
                    )
                )
                
                (then 
                    (if 
                        (is Line 3 Orthogonal exact:True)
                        
                        (moveAgain)
                    )
                )
            )
        )
        }
    )
    
    (rules 
        (start 
            (place "Marker" "Hand" count:12)
        )
        phases:{ 
        (phase "Placement" 
            (play 
                (if 
                    (is Prev Mover)
                    
                    (move Remove 
                        (sites Occupied by:Next container:"Board")
                    )
                    
                    (move 
                        (from 
                            (handSite Mover)
                        )
                        
                        (to 
                            (sites Empty)
                        )
                        
                        (then 
                            (if 
                                (is Line 3 Orthogonal exact:True)
                                
                                (moveAgain)
                            )
                        )
                    )
                )
            )
            
            (nextPhase Mover 
                (all Sites 
                    (sites Hand Mover)
                    if:
                    (= 0 
                        (count Cell at:
                            (site)
                        )
                    )
                )
            "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if 
                    (is Prev Mover)
                    
                    (move Remove 
                        (sites Occupied by:Next container:"Board")
                    )
                    
                    (forEach Piece)
                )
            )
        )
        }
        
        (end 
            (if 
                (no Pieces Next)
                
                (result Next Loss)
            )
        )
    )
)
---------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This alignment game, the name of which was not recorded, was played in the Ashanti Kingdom in the early nineteenth century. It was played on a wooden board with perforations, and the pieces were pegs to be inserted in the board.")
        (rules "The board is a series of intersecting lines in many directions, each line with three points. Players have equal numbers of pegs, and place their pieces at the same time. Players who place three of their pieces in a row remove one of the opponent's pieces. The player who removes all of the opponent's pieces wins.")
        (source "DLP evidence.")
        (id "1269")
        (version "1.3.6")
        (classification "board/space/line/reconstruction")
        (credit "Eric Piette")
        (origin "This game was played in Ashanti, around 1817.")
        }
    )
    
)
